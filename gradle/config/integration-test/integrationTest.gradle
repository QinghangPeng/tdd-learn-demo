// define the source set for integration test
sourceSets {
    integrationTest {
        java {
            java.srcDir file('src/integrationTest/java')

            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
        resources.srcDir file('src/integrationTest/resources')
    }
}

// define integration test configurations based on standard test configuration
configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

// define the integration test task
task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'
    systemProperty 'user.timezone', 'Asia/Singapore'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    useJUnitPlatform()
    shouldRunAfter test
}

// add the integration test task
check.dependsOn integrationTest
